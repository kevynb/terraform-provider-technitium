---
page_title: "godaddy-dns_record Resource - terraform-provider-godaddy-dns"
subcategory: ""
description: |-
  DNS resource record represens a single RR in managed domain
---

# godaddy-dns_record (Resource)

DNS resource record represens a single RR in managed domain

It currently supports `A`, `AAAA`, `CNAME`, `MX`, `NS` and `TXT` records. `SRV` are not supported; if anyone hosting AD on GoDaddy or uses SRV for VOIP or something like that, please let me know by creating an issue.

GoDaddy API does not have stable identities for DNS records, and in case of external modifications (e.g. via web console) behaviour is different for "single-valued" vs "multi-valued" records
- for "single-valued" record types (`A` and `CNAME`) there could be only 1 record of this type with a given name, so these are just replaced by update
- for "multi-valued" record types (`MX`, `NS`, `TXT`) there could be several records with a given name (e.g. multiple MXes with different priorities and targets), so matching is done on value; if record's value is modified outside of Terraform, it is treated as a completely different record and is preserved (and original record is considered gone), so record is re-created on update.

## Example Usage

```terraform
provider "godaddy-dns" {
  api_key    = "better set it in GODADDY_API_KEY"
  api_secret = "better set it in GODADDY_API_SECRET"
}

# create "alias.test.com" as CNAME for "other.com"
resource "godaddy-dns_record" "my-cname" {
  domain = "test.com"
  type   = "CNAME"
  name   = "alias"
  data   = "other.com"
}
```

To set several records, use `for_each`:

```terraform
terraform {
  required_providers {
    godaddy-dns = {
      source = "registry.terraform.io/veksh/godaddy-dns"
    }
  }
}

# keys from env
provider "godaddy-dns" {}

# struct for several records
locals {
  domain = "mydomain.com"
  records = {
    "mx" = {
      type = "MX",
      name = "_test-cli",
      data = "mx1.pseudo.com",
      prio = 10,
    },
    "txt" = {
      type = "TXT",
      name = "_test-cli",
      data = "also, txt",
    },
  }
}

# with names like `godaddy-dns_record.array-of-records["mx"]`
resource "godaddy-dns_record" "array-of-records" {
  for_each = local.records
  domain   = local.domain
  type     = each.value.type
  name     = each.value.name
  data     = each.value.data
  priority = lookup(each.value, "prio", null)
}
```

Use it in DNS challenge for Amazon Certificate Manager for Cloudfront site on custom domain:

```terraform
locals {
  website_name   = "mysite"
  website_domain = "mydomain.com"
}

# cloudfront requires (custom) cert to be in "us-east-1" region
provider "aws" {
  alias  = "us-east-1"
  region = "us-east-1"
}

# expects GODADDY_API_KEY and GODADDY_API_SECRET in environment
provider "godaddy-dns" {}

resource "aws_acm_certificate" "nondefault_cert" {
  provider          = aws.us-east-1
  domain_name       = local.website_name
  validation_method = "DNS"
  lifecycle {
    create_before_destroy = true
  }
}

resource "godaddy-dns_record" "cert_challenge" {
  for_each = {
    for dvo in aws_acm_certificate.nondefault_cert.domain_validation_options :
    dvo.domain_name => {
      name = trimsuffix(dvo.resource_record_name, join("", [".", local.website_domain, "."]))
      data = trimsuffix(dvo.resource_record_value, ".")
    }
  }

  domain = local.website_domain
  type   = "CNAME"
  name   = each.value.name
  data   = each.value.data
}

resource "aws_acm_certificate_validation" "nondefault_cert_valid" {
  provider        = aws.us-east-1
  certificate_arn = aws_acm_certificate.nondefault_cert.arn
  validation_record_fqdns = [
    for dvo in aws_acm_certificate.nondefault_cert.domain_validation_options :
  dvo.resource_record_name]
}

resource "aws_cloudfront_distribution" "s3_distribution" {
  aliases = [local.website_name]
  viewer_certificate {
    acm_certificate_arn = aws_acm_certificate_validation.nondefault_cert_valid.certificate_arn
    ssl_support_method  = "sni-only"
  }

  # rest of config is skipped, these are only for lint to stop complaining
  enabled = true
  restrictions {
    geo_restriction {
      restriction_type = ""
    }
  }
  origin {
    origin_id   = 1
    domain_name = ""
  }
  default_cache_behavior {
    allowed_methods        = [""]
    cached_methods         = [""]
    target_origin_id       = ""
    viewer_protocol_policy = ""
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `data` (String) Record value returned for DNS query: target for CNAME, ip address for A etc
- `domain` (String) Name of main managed domain (top-level) for this RR
- `name` (String) Record name name (part of FQN), may include `.` for records in sub-domains or be `@` for top-level records
- `type` (String) Resource record type: A, CNAME etc

### Optional

- `priority` (Number) Record priority, required for MX (lower is higher)
- `ttl` (Number) Record time-to-live, >= 600s <= 604800s (1 week), default 3600 seconds (1 hour)

## Import

Import is supported using the id in format `<domain>:<type>:<name>:<data>`:

```shell
terraform import godaddy-dns_record.cname-alias mydom.com:CNAME:alias:test.com
```
